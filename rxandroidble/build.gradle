plugins {
    id 'com.android.library'
    id 'groovyx.android'
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')



android {
    namespace = 'io.nrbtech.rxandroidble'

    compileSdk = libs.versions.compileSDK.get().toInteger()

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        minSdkVersion libs.versions.minSDK.get().toInteger()
        targetSdk = libs.versions.targetSDK.get().toInteger()
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.targetCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    publishing {
        singleVariant('release')
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
    lint {
        abortOnError = false
    }


    preBuild.dependsOn 'checkstyle'
}

// add explicit dependencies to prevent warnings
afterEvaluate {
    tasks.named('testDebugUnitTest') {
        dependsOn 'compileDebugUnitTestGroovyWithGroovyc'
    }
    tasks.named('testReleaseUnitTest') {
        dependsOn 'compileReleaseUnitTestGroovyWithGroovyc'
    }
}

androidGroovy {
    skipJavaC = true
}

dependencies {
    api libs.io.reactivex.rxjava3.rxjava
    api libs.androidx.annotation
    implementation libs.com.jakewharton.rxrelay3.rxrelay
    implementation project(path: ':dagger-library-shadow', transitive: false, configuration: 'shadow')
    annotationProcessor project(path: ':dagger-compiler-shadow', transitive: false, configuration: 'shadow')

    // Test dependencies
    testImplementation project(path: ':dagger-library-shadow', transitive: false, configuration: 'shadow')
    testImplementation libs.org.junit.jupiter.junit.jupiter
    testImplementation libs.org.junit.platform.junit.platform.launcher
    testImplementation libs.org.apache.groovy
    testImplementation libs.org.spockframework.spock.core
    testRuntimeOnly libs.net.bytebuddy.byte.buddy
    testRuntimeOnly libs.org.objenesis
}
