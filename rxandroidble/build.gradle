apply plugin: 'com.android.library'
apply plugin: 'groovyx.android'
apply from: rootProject.file('gradle/gradle-mvn-push.gradle')

android {
    namespace 'io.nrbtech.rxandroidble'

    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 33
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.targetCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    publishing {
        singleVariant('release')
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
    lint {
        abortOnError false
    }


    preBuild.dependsOn 'checkstyle'
}

// add explicit dependencies to prevent warnings
afterEvaluate {
    tasks.named('testDebugUnitTest') {
        dependsOn 'compileDebugUnitTestGroovyWithGroovyc'
    }
    tasks.named('testReleaseUnitTest') {
        dependsOn 'compileReleaseUnitTestGroovyWithGroovyc'
    }
}

androidGroovy {
    skipJavaC = true
}

dependencies {
    api rootProject.ext.libs.rxjava
    api rootProject.ext.libs.annotations
    implementation rootProject.ext.libs.rxrelay
    implementation project(path: ':dagger-library-shadow', transitive: false, configuration: 'shadow')
    annotationProcessor project(path: ':dagger-compiler-shadow', transitive: false, configuration: 'shadow')

    // Test dependencies
    testImplementation project(path: ':dagger-library-shadow', transitive: false, configuration: 'shadow')
    testImplementation rootProject.ext.libs.junit
    testImplementation rootProject.ext.libs.groovy
    testImplementation rootProject.ext.libs.spock
}
