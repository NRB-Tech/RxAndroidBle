apply plugin: 'com.android.library'
apply plugin: 'groovyx.android'
apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
apply plugin: 'com.github.johnrengelman.shadow'

android {
    namespace 'io.nrbtech.rxandroidble.mockrxandroidble'

    compileSdk libs.versions.compileSDK.get().toInteger()

    defaultConfig {
        minSdkVersion libs.versions.minSDK.get().toInteger()
        targetSdk libs.versions.targetSDK.get().toInteger()
    }

    compileOptions {
        sourceCompatibility rootProject.ext.targetCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    publishing {
        singleVariant('release')
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }


    preBuild.dependsOn 'checkstyle'

    sourceSets {
        test {
            java {
                srcDir("../rxandroidble/src/test/java") // Add android stubs from main lib tests
            }
        }
    }
    lint {
        abortOnError false
    }
}

// add explicit dependencies to prevent warnings
afterEvaluate {
    tasks.named('testDebugUnitTest') {
        dependsOn 'compileDebugUnitTestGroovyWithGroovyc'
    }
    tasks.named('testReleaseUnitTest') {
        dependsOn 'compileReleaseUnitTestGroovyWithGroovyc'
    }
}

dependencies {

    // Test dependencies
    testImplementation libs.org.junit.jupiter.junit.jupiter
    testImplementation libs.org.junit.platform.junit.platform.launcher
    testImplementation libs.org.apache.groovy
    testImplementation libs.org.spockframework.spock.core
    api project(path: ':rxandroidble')
    api libs.io.reactivex.rxjava3.rxjava
}
