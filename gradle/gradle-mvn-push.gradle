/*
 * Copyright 2013 Chris Banes
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'maven-publish'
apply plugin: 'signing'

version = VERSION_NAME
group = GROUP

afterEvaluate { project ->
    if (isAndroidProject(project)) {
        android.libraryVariants.matching { it.name == "release" }.each { releaseVariant ->
            task javadocs(type: Javadoc, dependsOn: ['assembleRelease', project.configurations.doclava]) {
                source = android.sourceSets.main.java.source
                classpath += project.files(
                        files(releaseVariant.javaCompileProvider.get().classpath),
                        project.android.getBootClasspath().join(File.pathSeparator),
                        files("$buildDir/intermediates/classes/release"),
                        files("$buildDir/generated/ap_generated_sources/release/out"),
                        files("$project.rootProject.rootDir/dagger-library-shadow/build/libs/dagger-library-shadow-${project.version}.jar")
                )
                title = null
                options.noTimestamp(false)
                options.doclet = "com.google.doclava.Doclava"
                options.docletpath = project.configurations.doclava.files.asType(List)
            }
        }
    } else {
        task javadocs(type: Javadoc) {
            source = sourceSets.main.java.classesDirectory
            classpath += configurations.compileClasspath
            title = null
            options.noTimestamp(false)
            options.doclet = "com.google.doclava.Doclava"
            options.docletpath = configurations.doclava.files.asType(List)
        }
    }
    task javadocsJar(type: Jar, dependsOn: javadocs) {
        archiveClassifier.set('javadoc')
        from javadocs.destinationDir
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        if (isAndroidProject(project)) {
            from android.sourceSets.main.java.source
        } else {
            from sourceSets.main.allSource
        }
    }

    publishing {
        publications {
            release(MavenPublication) {
                afterEvaluate {
                    from isAndroidProject(project) ? components.release : components.java
                    groupId = GROUP
                    artifactId = POM_ARTIFACT_ID
                    version = VERSION_NAME
                    artifact sourcesJar
                    artifact javadocsJar
                    pom {
                        name = POM_NAME
                        description = POM_DESCRIPTION
                        url = POM_URL
                        packaging = POM_PACKAGING
                        licenses {
                            license {
                                name = POM_LICENCE_NAME
                                url = POM_LICENCE_URL
                                distribution = POM_LICENCE_DIST
                            }
                        }
                        developers {
                            developer {
                                id = POM_DEVELOPER_ID
                                name = POM_DEVELOPER_NAME
                            }
                        }
                        scm {
                            url = POM_SCM_URL
                            connection = POM_SCM_CONNECTION
                            developerConnection = POM_SCM_DEV_CONNECTION
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        useInMemoryPgpKeys(getSigningKey(), getSigningKeyPassword())
        sign(publishing.publications.release)
    }
}